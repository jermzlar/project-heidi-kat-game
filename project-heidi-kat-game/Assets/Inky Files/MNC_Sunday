== MNC_sunday ==

~background = "security_station"
~music_cue = "ambient spaceship 1"

~whos_talking = "nobody"
You head in {not intro.security: what you believe to be} the direction of the security station. <>

{not intro.security:
~whos_talking = "nobody"
You aren't entirely sure, as you've had little cause to visit it over the last three months. Periodically you check the station schematics in your PDD to be sure you're still on the right path.
}

~whos_talking = "nobody"
The halls of Cassio are dimly lit by dim red emergency lighting. Scorch marks line some of the walls, and in places there are pieces of plasteel wall and ceiling sections hanging precariously. As you walk, you note that you see very few bodies. There is one or two here and there, but not nearly as many as you would expect from a disaster like this. Did most people already get off the station? you wonder to yourself. How long was I out?

~whos_talking = "nobody"
~sound_cue = "metal footsteps"
You take a set of stairs down in almost total darkness, and at the bottom you come to the lobby of the security station. The lights here are flickering and the front desks are on their sides, their contents unceremoniously spilled out onto the floor.

~whos_talking = "nobody"
Beyond the desks lies the door to the offices. It's closed, and there doesn't appear to be anyone here.

{intro.security:

~whos_talking = "nobody"
You glance at the overturned desk beside you. This was Lulah's desk. You wonder briefly if she is okay until your eyes fall on her lifeless body in the corner, covered in blood and missing part of her leg. Guess not.

-else:

~whos_talking = "nobody"
You glance at the overturned desk beside you, and your eyes travel to a lifeless body in the corner beside it: a woman, covered in blood and missing part of her leg.
}

{not hospital_ward:

~whos_talking = "nobody"
~sound_cue = "woozy"
A wave of dizziness rushes over you, and you place a hand on the adjacent wall to steady yourself. This head wound of yours likely needs some attention soon. You'd better be careful.
}

~whos_talking = "nobody"
A sudden noise from behind the door drags your attention to the offices beyond. It sounds like someone is in there.

~whos_talking_button = "nobody"
    * [Investigate.]
        You open the door and have a few moments to take stock of the situation. There is a young woman stooped over in the far corner, apparently rifling through a security officer's desk.
    -> enter_office
    
    * [Try to take a peak through a crack in the door.]
    -> peakin

= peakin

~whos_talking = "nobody"
You crack the door open just a touch, looking through the tiny opening to take stock of the situation. There is a young woman stoop over in the far corner. She appears to be rifling through a security officer's desk. 

-> enter_office

= enter_office

~character_a = "sunday"
The woman stops abruptly and turns to look up at you, freezing. She appears to be waiting to see how you'll react.

    ~whos_talking_button = "mc"
    * ["I don't think you're supposed to be doing that."]
        "I don't think you're supposed to be doing that," you say blithely.
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "Well, I don't think you're supposed to be back here either, sweetheart, so let's just promise not to tell on one another and call it even, okay?" the woman replies coyly.
    
    ~whos_talking_button = "mc"
    *"Are you allowed to be in here?" 
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "I think the rules concerning who is allowed where went out the window with the initial explosion, don't you?" the woman replies coolly.

-
~whos_talking = "nobody"
~sound_cue = "rummage"
She returns her attention to the desk, pulling out one of the drawers and dumping its contents on the floor. She knocks on the bottom of the drawer rapidly. You are about to address her again when she suddenly lets out a triumphant, "Aha!" She proceeds to remove a false bottom, reaches underneath, and pulls out a small device.

    ~whos_talking_button = "mc"
    *["Find what you were looking for?"]
        "Find what you were looking for?" you ask.
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "I did, not that I think you're terribly invested in what I'm doing here," the woman responds.
        ~whos_talking = "mc"
        "And what are you doing here?"
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "That's classified information," she replies.

    ~whos_talking_button = "mc"
    *["I'm pretty sure that's classified information."]
        "I'm pretty sure that's classified information," you remark to her.
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "It is, not that I think you're terribly invested in what's going on here," the woman responds.
        ~whos_talking = "mc"
        "And what is going on here?"
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "Now that's classified information," she replies.

-
~whos_talking = "nobody"
The woman slips the small device she's recovered into one of the pockets on her jumpsuit, and then surveys you seriously.

~whos_talking = "stranger"
~character_a = "sunday"
"What's you name, love?" she asks.

    ~whos_talking = "nobody"
    *"{player_name}."
    
    ~whos_talking = "nobody"
    *(sour) ["Definitely not 'love.'"]
        "Definitely not 'love.' Call me {player_name} or nothing at all."
        ~rel_sunday -=5

-
{sour:

~whos_talking = "sunday"
~character_a = "sunday"
"Feeling disagreeable, are we? Well, regardless, you can call me Sunday.
}

{not sour:

~whos_talking = "sunday"
~character_a = "sunday"

"A pleasure to meet you, {player_name}. My name is Sunday.
}

- <> I suppose you work on this station?" she asks.

You nod.

"And you awoke recently I take it. I thought everyone was dead or gone, yet here you are. You must have some questions."

"Yeah," you start, wondering what to ask first.

-> questions

= questions

~character_a = "sunday"

    ~whos_talking_button = "mc"
    *["What happened?"]
        "What happened, exactly?" you ask.
        ~whos_talking = "sunday"
        "You mean the explosion? Nearly every power decoupler on the station blew," Sunday says as if it's the most obvious thing in the world.
            {intro.technician:
                "How?" you say incredulously. "That would take a simultaneous equipment failure of impossible magnitude; there's failsafes for that in the event of a station-wide malfunction."
                ~whos_talking = "sunday"
                "Best not to think too long on that one, {sour: darling|love}," Sunday says, a touch of sadness at the edge of her cheerful disposition.
                
                -else:
                "That seems an unlikely string of events," you say suspiciously.
                ~whos_talking = "sunday"
                "Best not to think too long on that one, {sour: darling|love}," Sunday says cryptically.
                }

    ~whos_talking_button = "mc"
    *["What are you doing here?"]
        "Just what are you doing here?"
        ~whos_talking = "sunday"
        "I told you, I can't tell you that," Sunday replies.
        
            ~whos_talking_button = "mc"
            *{not intro.security} ["Can't or won't?"]
                "Can't or won't?" you ask.
                ~whos_talking = "nobody"
                Sundays shrugs.
                ~whos_talking = "sunday"
                "Does it matter? Either way, you're not getting anything out of me."
                
            ~whos_talking_button = "mc"
            *["Whatever it is, I can tell it's illegal."]
                "Whatever it is, I can tell it's illegal," you say accusingly.
                ~whos_talking = "sunday"
                "And what are you going to do about that, hm? Call station security?" Sunday asks, clearly not threatened in the slightest.
                    {intro.security:
                    ~whos_talking = "mc"
                    "I'm part of station security," you say.
                    ~whos_talking = "nobody"
                    Sunday looks you over appraisingly.
                    ~whos_talking = "sunday"
                    "You are station security from the look of it, {sour: darling|love}," she says seriously.
                    }
            
            ~whos_talking_button = "mc"
            *(sunday_elysium) {intro.security}["Give me one good reason I shouldn't arrest you."]
                "Give me one good reason I shouldn't arrest you right here," you threaten her.
                ~whos_talking = "sunday"
                "Let's see, does the fact that Cassio has been completely abandoned by those who granted you that authority about cover it?" Sunday says as she taps her chin, and then she pretends to appear to have a sudden idea. "Or, no, how about the fact that I {not sunday_who: also work for Elysium and} completely outrank you in every sense of the word?"
                ~whos_talking = "mc"
                "If you work for Elysium, then why are you--?" you start, but she interrupts you.
                ~whos_talking = "sunday"
                "What did I tell you about classified information, {sour: darling|love}?"
                
            -~whos_talking = "nobody"
            You sigh. Apparently you aren't going to win this one.

    ~whos_talking_button = "mc"
    *[Where is everyone?"]
        "Where did everyone go? There should be more people here," you say.
        ~whos_talking = "sunday"
        "Gone, mostly. They either died in the initial explosion or they've been taken since then," Sunday explains.~whos_talking = "mc"
        "What do you mean, 'taken since then?' It just happened."
        ~whos_talking = "nobody"
        Sunday fixes you with a sympathetic gaze.
        ~whos_talking = "sunday"
        "We should probably get your head look at. The initial explosion was days ago," she explains.
        ~whos_talking = "mc"
        "But it just..." you trail off.
        ~whos_talking = "nobody"
        Sunday is starting to look a bit concerned. Best to leave it alone for now.

    ~whos_talking_button = "mc"
    *(sunday_who)["Who are you, again?"]
        "And who are you, again?" you ask.
        ~whos_talking = "sunday"
        "Sunday Lien, at your service," Sunday says with a little bow. "Not literally, of course. If anything, you're at my service."
        ~whos_talking = "nobody"
        You snort.
        ~whos_talking = "mc"
        "And how's that, now?"
        ~whos_talking = "sunday"
        "What, you don't recognize a fellow employee when you see one?" Sunday says, affecting a hurt tone. "I suppose it makes sense, my position is at the top and very confidential within the company, and they do employ hundreds of thousands of people across the system, but still."
            
            ~whos_talking_button = "mc"
            *"What do you do for Elysium?"
                "What is it you do for Elysium, then?" you ask.
                ~whos_talking = "sunday"
                "What have I told you about classified information, {sour: darling|love}?" Sunday says, slightly chastising.
            
            ~whos_talking_button = "mc"
            *["I don't believe you."]
                "I'm not sure I believe you," you say, suspicion leaking into your voice.
                ~whos_talking = "sunday"
                "No one's asking you to, {sour: darling|love}, but it doesn't change what's facts," Sunday replies nonchalantly.
            
            
            -
            ~whos_talking = "nobody"
            You roll your eyes. She's obviously not intent on giving up more than that.
        
    
    *(done)[That's all for now.]
    
-
{not done:
~whos_talking = "nobody"
Would you like to ask anything else?

-> questions

-else:

~whos_talking = "nobody"
You sigh, quite tired of being run around in circles. That's probably the most you're going to get out of her, at least for now.

->sunday_join
}

= sunday_join

~whos_talking = "sunday"
~character_a = "sunday"
"Now, it's my turn," Sunday says, clapping her hands together. "I can't pretend to know exactly what your plan is, but I'll tell you this: if it's to get off of this station intact, our best bet is to partner up."


    ~whos_talking_button = "mc"
    *["Fine, but I don't trust you."]
        "Fine, but I don't trust you," you say flatly.
        ~rel_sunday -= 10
        ~communal_independent += 10
        ~whos_talking = "sunday"
        ~character_a = "sunday"
        "{sour:Darling|Love}, I'd keep in mind if I were you that you need me much more than I need you at this moment in time," Sunday says, and though you won't admit it outloud, you're hard-pressed to refute her claim.
    
    ~whos_talking_button = "mc"
    *["That sounds like a good idea."]
    "That sounds like a good idea," you say. "Maybe together we can find a safe way off this mess."
    ~whos_talking = "sunday"
    ~character_a = "sunday"
    "Splendid. Glad to have you on board," Sunday replies, and you can't quite tell if she's being sincere or not. You have a feeling that will be happening often with her.
        ~rel_sunday += 10
        ~communal_independent -= 10

-
~whos_talking = "sunday"
~character_a = "sunday"
"There is one more thing you should know," she adds. "We have a fairly limited amount of time before the damage to Cassio compounds and causes what will be a decidedly volatile reaction," she says, crossing the room to you and coming to a halt at your side. "Well, moreso than the first one. So, I suggest we get moving."

{intro.help_scared_injured:
 ~whos_talking = "mc"
"That means it's doubly important that we get as many people off this station with us as possible," you say emphatically.
Sunday laughs hollowly.
"There's no one left."
"Cassio is huge; there's got to be some people left," you argue.
}

{intro.get_tf_outta_here:
 ~whos_talking = "mc"
"Our plan is still to get out of here; the only thing that changes is we're going to have to do it quickly. Or die," you say matter-of-factly.
}

{intro.find_out_wha_happe:
 ~whos_talking = "mc"
"Which means we've only got a short time to gather evidence and figure out who did this," you say grimly.
}

{intro.get_phat_loots:
 ~whos_talking = "mc"
"I guess that means we'll have to be quick about finding the valuables, then," you say spiritedly.

~whos_talking = "sunday"
"So glad you have your priorities set right," Sunday mutters.

~whos_talking = "nobody"
You shrug.
}

~whos_talking = "sunday"
~character_a = "sunday"
"Well, first things first, if you want to <>

{intro.get_tf_outta_here:

~whos_talking = "sunday"
~character_a = "sunday"
get off this space junk, we'll need to hit the laboratory. That's the only place that still has working computers with station-wide access, and we'll need that access to authorize departure for the short-range shuttles," Sunday explains.

~whos_talking = "mc"
"Wait, why do we need to authorize departure for shuttles? The station is in a state of emergency, their departure should be top priority," you say suspiciously.

~whos_talking = "nobody"
Sunday fixes you with a pitying look.

~whos_talking = "sunday"
~character_a = "sunday"
"The fewer questions you ask about that, the better," is all she says in response.
}

{intro.technician and intro.get_tf_outta_here:

~whos_talking = "mc"
"Even so, what about Operations? I have authorized access," you say, indicating the small stitched label that reads "OPERATIONS" on your uniform.

~whos_talking = "sunday"
~character_a = "sunday"
"Operations is toast, {sour: darling|sweetheart}. It's not much more than a scorched hole in the wall."

~whos_talking = "mc"
"If you say so. <>
}

{intro.get_phat_loots:

~whos_talking = "sunday"
~character_a = "sunday"
figure out where the expensive stuff is, we should hit the laboratory. It's one of the few places here still relatively intact, and I can guarantee there's a wealth of research there that certain people I'm acquainted with would pay not insignificant sums of credits to obtain."

"<>
}

{intro.get_phat_loots and intro.scientist:

~whos_talking = "mc"
"That I already know," you reply, indicating the small stitched label that reads "SCIENCE" on your uniform. "<>
}

{intro.help_scared_injured:

~whos_talking = "sunday"
~character_a = "sunday"
help whoever's left, we need to swing by the laboratory. That's the only place that still has working computers with station-wide access, and we'll need that access to authorize departure for the short-range shuttles," Sunday explains.

~whos_talking = "mc"
"Wait, why do we need to authorize departure for shuttles? The station is in a state of emergency, their departure should be top priority," you say suspiciously.

~whos_talking = "nobody"
~character_a = "sunday"
Sunday fixes you with a pitying look.

~whos_talking = "sunday"
"The fewer questions you ask about that, the better," is all she says in response.

~whos_talking = "mc"
"If you say so. <>
}

{intro.find_out_wha_happe:

~whos_talking = "sunday"
~character_a = "sunday"
figure out what happened, we need to hit the laboratory. That's the only place that still has working computers with station-wide access, and we'll need that access to gather any pertinent information. We'll also need it to authorize departure for the short-range shuttles," Sunday explains.

~whos_talking = "mc"
"Wait, why do we need to authorize departure for shuttles? The station is in a state of emergency, their departure should be top priority," you say suspiciously.

~character_a = "sunday"
~whos_talking = "nobody"
Sunday fixes you with a pitying look.

~whos_talking = "sunday"
"The fewer questions you ask about that, the better," is all she says in response.

~whos_talking = "mc"
"If you say so. <>
}

~whos_talking = "nobody"
~sound_cue = "woozy"
Your vision blurs for a moment and you feel yourself almost stumble forward as Sunday catches you by the arm.

~whos_talking = "sunday"
~character_a = "sunday"
"First things first, though, I think we need to get you to the hospital ward and take care of that wound on your head," she says.

~whos_talking = "mc"
"You're probably right. {intro.doctor or intro.combat_medic or intro.unconventional_medic or intro.medicine: I'll be able to use the supplies there to patch myself up|Hopefully there will be someone there who can help}."

~whos_talking = "sunday"
~character_a = "sunday"
"Let's hope so, because my skillset leans more towards the opposite of medicine," Sunday quips as the two of you exit the offices.

~character_a = "nobody"

~music_cue = "music fade"

-> transitions.security_to_lab_or_hosp








