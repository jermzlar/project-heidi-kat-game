== MNC_sunday ==

~background = "security_station"

~whos_talking = "nobody"
You head in {not intro.security: what you believe to be} the direction of the security station. <>

{not intro.security:
~whos_talking = "nobody"
You aren't entirely sure, as you've had little cause to visit it over the last three months of being stationed here. Periodically you check the station schematics in your PDD to be sure you're still on the right path.
}

~whos_talking = "nobody"
The halls of Cassio are dimly lit by dim red emergency lighting. Scorch marks line some of the walls, and in places there are pieces of plasteel wall and ceiling sections precariously hanging. As you walk, you note that you see very few people. There is a body or two here and there, but not nearly as many as you would expect from a disaster like this. Did most people already get off the station? you wonder to yourself. How long was I out?

~whos_talking = "nobody"
You take a set of stairs down in almost total darkness, and at the bottom you come to the lobby of the security station. The lights here are flickering and the front desks are on their sides, their contents unceremoniously spilled out onto the floor.

~whos_talking = "nobody"
Beyond the desks lies the door to the offices. It's closed, and there doesn't appear to be anyone here.

{intro.security:

~whos_talking = "nobody"
You glance at the overturned desk beside you. This was Lulah's desk. You wonder briefly if she is okay until your eyes fall on her lifeless body in the corner, covered in blood and missing part of her leg. Guess not.

-else:

~whos_talking = "nobody"
You glance at the overturned desk beside you, and your eyes travel to the lifeless body in the corner beside it. A woman, covered in blood and missing part of her leg.
}

{not hospital_ward:

~whos_talking = "nobody"
A wave of dizziness rushes over you, and you place a hand on the adjacent wall to steady yourself. This head wound of yours likely needs some attention soon. You'd better be careful.
}

~whos_talking = "nobody"
A sudden noise from behind the door drags your attention to the offices beyond. It sounds like someone is in there.

~whos_talking_button = "nobody"
    * [Investigate.]
        You open the door and have a few moments to take stock of the situation. There is a young woman standing in the far corner, apparently rifling through a security officer's desk.
    -> enter_office
    
    * [Try to take a peak through a crack in the door.]
    -> peakin

= peakin

~whos_talking = "nobody"
You crack the door open just a touch, looking through the tiny opening to take stock of the situation. There is a young woman standing in the far corner. She appears to be rifling through a security officer's desk. 

-> enter_office

= enter_office

~character_a = "sunday"
The woman stops abruptly and turns to look up at you, freezing. She appears to be waiting to see how you'll react.

    ~whos_talking_button = "mc"
    * ["I don't think you're supposed to be doing that."]
        "I don't think you're supposed to be doing that," you say blithely.
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "Well, I don't think you're supposed to be back here either, sweetheart, so let's just promise not to tell on one another and call it even, okay?" the woman replies coolly.
    
    ~whos_talking_button = "mc"
    *"Are you allowed to be in here?" 
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "I think the rules concerning who is allowed where went out the window with the initial explosion, don't you?" the woman replies coolly.

-
~whos_talking = "nobody"
She returns her attention to the desk, pulling out one of the drawers and dumping its contents on the floor. She knocks on the bottom of the drawer rapidly, and you are about to address her again until she lets out a triumphant, "Aha!" She appears to remove a false bottom, reaching underneath and pulling out a small device.

    ~whos_talking_button = "mc"
    *["Find what you were looking for?"]
        "Find what you were looking for?" you ask.
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "I did, not that I think you're terribly invested in what I'm doing here," the woman responds.
        ~whos_talking = "mc"
        "And what are you doing here?"
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "That's classified information," she replies.

    ~whos_talking_button = "mc"
    *["I'm pretty sure that's classified information."]
        "I'm pretty sure that's classified information," you remark to her.
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "It is, not that I think you're terribly invested in what's going on here," the woman responds.
        ~whos_talking = "mc"
        "And what is going on here?"
        ~whos_talking = "stranger"
        ~character_a = "sunday"
        "Now that's classified information," she replies.

-
~whos_talking = "nobody"
The womans slips the small device she's recovered into one of the pockets on her jumpsuit, and then she surveys you seriously.

~whos_talking = "stranger"
~character_a = "sunday"
"What's you name, sweetheart?" she asks.

    ~whos_talking = "nobody"
    *"{player_name}."
    
    ~whos_talking = "nobody"
    *(sour) ["Definitely not sweetheart."]
        "Definitely not 'sweetheart.' Call me {player_name} or nothing at all."
        ~rel_sunday -=5

-
{sour:

~whos_talking = "sunday"
~character_a = "sunday"
"Feeling disagreeable, are we? Well, regardless, you can call me Sunday. I don't know what your plan is exactly, but I don't suppose you'd want to join forces?"
}

{not sour:

~whos_talking = "sunday"
~character_a = "sunday"

"A pleasure to meet you, {player_name}. My name is Sunday. I'm not sure what your plan is exactly, but would you be interested in joining forces?"
}

    *["Fine, but I don't trust you."]
        "Fine, but I don't trust you," you say suspiciously.
        ~rel_sunday -= 10
        ~communal_independent += 10
        
    *["That sounds like a good idea."]
    "That sounds like a good idea," you say. "Maybe together we can {intro.help_scared_injured: help out those who need it}{intro.get_tf_outta_here: find a safe way off this mess}{intro.find_out_wha_happe: figure out who's responsible for this}{intro.get_phat_loots: take a bigger haul}."
        ~rel_sunday += 10
        ~communal_independent -= 10

-
~whos_talking = "sunday"
~character_a = "sunday"
"Splendid. Glad to have you on board," Sunday replies, and you can't tell if she's being sincere or not. You have a feeling that will be happening often with her.

~whos_talking = "sunday"
~character_a = "sunday"
"There is one more thing you should know," she adds. "We have a fairly limited amount of time before the damage to Cassio compounds and causes what will be a decidedly volatile reaction," she says, crossing the room to you and coming to a halt at your side. "Well, moreso than the first one. So, I suggest we get moving."

{intro.help_scared_injured:
 ~whos_talking = "mc"
"That means it's doubly important that we get as many people off this station with us as possible," you say emphatically.
}

{intro.get_tf_outta_here:
 ~whos_talking = "mc"
"Our plan is still to get out of here; the only thing that changes is we're going to have to do it quickly. Or die," you say matter-of-factly.
}

{intro.find_out_wha_happe:
 ~whos_talking = "mc"
"Which means we've only got a short time to gather evidence and figure out who did this," you say grimly.
}

{intro.get_phat_loots:
 ~whos_talking = "mc"
"I guess that means we'll have to be quick about finding the valuables, then," you say spiritedly.
}

{intro.get_phat_loots:

~whos_talking = "sunday"
"So glad you have your priorities set right," Sunday mutters.

~whos_talking = "nobody"
You shrug.

-else:

~whos_talking = "nobody"
Sunday nods.
}

~whos_talking = "sunday"
~character_a = "sunday"
"Well, first things first, if you want to <>

{intro.get_tf_outta_here:

~whos_talking = "sunday"
~character_a = "sunday"
get off this space junk, we'll need to hit the laboratory. That's the only place that still has working computers with station-wide access, and we'll need that access to authorize departure for the short-range shuttles."

~whos_talking = "mc"
"Wait, why do we need to authorize departure for shuttles? The station is in a state of emergency, their departure should be top priority," you say suspiciously.

~whos_talking = "nobody"
Sunday fixes you with a pitying look.

~whos_talking = "sunday"
~character_a = "sunday"
"The fewer questions you ask about that, the better," is all she says in response.
}

{intro.technician and intro.get_tf_outta_here:

~whos_talking = "mc"
"Even so, what about Operations? I have authorized access," you say, indicating the small stitched label that reads "OPERATIONS" on your uniform.

~whos_talking = "sunday"
~character_a = "sunday"
"Operations is toast, {sour: darling|sweetheart}. It's not much more than a scorched hole in the wall."

~whos_talking = "mc"
"If you say so. <>
}

{intro.get_phat_loots:

~whos_talking = "sunday"
~character_a = "sunday"
figure out where the expensive stuff is, we should hit the laboratory. It's one of the few places here still relatively intact, and I can guarantee there's a wealth of research there that certain people I'm acquainted with would pay not insignificant sums of credits to obtain."

"<>
}

{intro.get_phat_loots and intro.scientist:

~whos_talking = "mc"
"That I already know," you reply, indicating the small stitched label that reads "SCIENCE" on your uniform. "<>
}

{intro.help_scared_injured:

~whos_talking = "sunday"
~character_a = "sunday"
help whoever's left, we need to swing by the laboratory. That's the only place that still has working computers with station-wide access, and we'll need that access to authorize departure for the short-range shuttles."

~whos_talking = "mc"
"Wait, why do we need to authorize departure for shuttles? The station is in a state of emergency, their departure should be top priority," you say suspiciously.

~whos_talking = "nobody"
~character_a = "sunday"
Sunday fixes you with a pitying look.

~whos_talking = "sunday"
"The fewer questions you ask about that, the better," is all she says in response.

~whos_talking = "mc"
"If you say so. <>
}

{intro.find_out_wha_happe:

~whos_talking = "sunday"
~character_a = "sunday"
figure out what happened, we need to hit the laboratory. That's the only place that still has working computers with station-wide access, and we'll need that access to gather any pertinent information. We'll also need it to authorize departure for the short-range shuttles."

~whos_talking = "mc"
"Wait, why do we need to authorize departure for shuttles? The station is in a state of emergency, their departure should be top priority," you say suspiciously.

~character_a = "sunday"
~whos_talking = "nobody"
Sunday fixes you with a pitying look.

~whos_talking = "sunday"
"The fewer questions you ask about that, the better," is all she says in response.

~whos_talking = "mc"
"If you say so. <>
}

~whos_talking = "nobody"
Your vision blurs for a moment and you feel yourself almost stumble forward as Sunday catches you by the arm.

~whos_talking = "sunday"
~character_a = "sunday"
"First things first, though, I think we need to get you to the hospital ward and have your head looked at," she says.

~whos_talking = "mc"
"You're probably right. We'll head for the ward. {intro.doctor or intro.combat_medic or intro.unconventional_medic or intro.medicine: I'll be able to use the supplies there to patch myself up|Hopefully there will be someone there who can help}."

~whos_talking = "sunday"
~character_a = "sunday"
"Let's hope so, because my skillset leans more towards the opposite of medicine and healing," Sunday quips as the two of you exit the offices.

-> transitions.security_to_lab_or_hosp







-> END