== the_discovery ==
~background = "laboratory_zoomin"
You and Sunday enter the room adjacent to the laboratory. The lights here, like everywhere else on the station, are the dim green emergency lights that flicker periodically. There are about five computers still working here, each looking very similarly perched on top of identical desks. Of the other three inoperable computers, one has a cracked screen, one has collided with the floor, and one appears to be smoking faintly. {intro.scientist: You are slightly pained at the sight of the data room, having been previously regaled by fellow scientists about how many years it took each of them to convince Elysium to grant funding for various equipment replacements and upgrades.}

"What did Lin mean when they said we'd both find what we're looking for in here?" you ask Sunday.

~whos_talking = "sunday"
~character_a = "Sunday"
"Haven't the faintest idea," Sunday says; although, you're fairly certain she is lying.

*"I'm really tired of being kept in the dark[."] here," you huff.
~whos_talking = "sunday"
~character_a = "Sunday"
"Get used to it," she says with all the empathy of a sea cucumber.
*"Even if you don't tell me, I'm going to find out[."] when you find it here," you point out.
~whos_talking = "sunday"
~character_a = "Sunday"
"Not if I can help it," she replies{rel_sunday >65:, then adds, "I'm looking out for you, you know."|.}

-The two of you fall silent as Sunday sets about searching the room. She, for her part, seems to know exactly where she's going as she heads towards a desk at the far corner with a computer. She sits and almost immediately begins typing and clicking furiously.

You likewise seat yourself at one of the desks and begin to enter your station log-in information. <>

{intro.security or intro.technician or intro.scientist: With your credentials, you should have no trouble authorizing shuttle departure from here.}
{intro.doctor or intro.xenolinguist:
With your credentials, you're not entirely sure you'll be able to authorize shuttle departure from here.
As though she read your mind, Sunday calls for your attention and tells you hers, adding, <>
~whos_talking = "sunday"
~character_a = "Sunday"
"You'll need that to authorize departure."
} <>
Upon signing on to the station hub, it takes you a moment to find the command controls you're looking for. 

TODO: add different formatting via CSS for computer text

...

...

*[Select global authorization controls for short-range shuttle bay.] -> departure_authorization

= departure_authorization
You are required to enter {intro.doctor or intro.xenolinguist: Sunday's|your} crendetials several more times as you navigate authorizing departures, but the relatively straightfoward process is finished in a few minutes.

"Okay, I think we're good to go," you say triumphantly to Sunday.

You glance back at the screen, and something catches your eye.

*There's a folder named "Jenusian Export Logs" here... -> export_logs

= export_logs
which is strange, considering the Jenusians have refused the majority of exports from Elysium since the war in '83. What could the company possibly be trading to them?

You open the folder and a long list of files appears, each with a syntax that includes what appears to be a person's name in addition to a recent date. Some of these names look familiar.

*(benjoe) [Select the file "BENJOE_DERSON"]
*(hallee) [Select the file "RENCE_HALLEE"]
*(sones) [Select the file "MATHY_SONES"]

-You click on the file to open it, and up comes a document that looks something like a profile of a person--a person you recognize, in fact. You remember {benjoe: Benjoe}{hallee: Hallee}{sones: Sones}; he lived in a room not more than three doors down from you. On your first day on the station he offered you a drink at one of the watering holes; you got to talking, and eventually you became good friends until about a month ago. One day you woke up and he was just... gone. When you asked around after him, you were told he was simply reassigned. It had been a bit of a blow that he hadn't bothered to tell you.
You scroll through the profile. It's a collection of physical descriptions, personality markers, mental health evaluations, and other personal points of data about {benjoe: Benjoe}{hallee: Hallee}{sones: Sones}.
Towards the bottom of the document are several shipping manifests indicating cargo exported from Cassio to another planet in the system, Valbaara II. But Valbaara II is a dead planet. You recall it was slated for terraforming, but after the war it was put on hold indefinitely. And the shipping manifests don't indicate the nature of the cargo, unless...
You're struggling to keep up with the speed of your realization as you scroll through additional profiles. You don't see any other way around it: Elysium, for whatever reason, is covering up the fact that they're disappearing their own low-level employees to some backwater planet in the system--though for what purpose remains to be seen. Sunday works for Elysium, does she know about this? you wonder.
It's a moment before you realize she has come up behind you as you hear her murmur, <>
~whos_talking = "sunday"
~character_a = "Sunday"
"Shit."

*[Confront Sunday--physically, if necessary.] ->physical_confront
*[Confront Sunday peacefully.] -> peaceful_confront

= physical_confront
You only have a few moments to react. In one swift move you jump to your feet and turn to face Sunday, holding your fists up in preparation for a fight. Sunday draws her weapon and immediately trains it on you, something you hadn't really considered. It appears you've brought your fists to a light-gun fight.
~whos_talking = "sunday"
~character_a = "Sunday"
"What the hell is this?" Sunday asks, motioning to you with the pistol.

*"I know what Elysium is doing[."], and I'm not going to let you take me," you say, accusation strong in your voice.
~whos_talking = "sunday"
~character_a = "Sunday"
"Take you? {MNC_sunday.sour: Darling|Sweetheart}, I'm the very last of your worries now that you know about this," Sunday almost laughs.
*"You work for Elysium; did you know about this?"[] you ask, accusation strong in your voice. 
~whos_talking = "sunday"
~character_a = "Sunday"
"I did," Sunday replies solemnly.

She drops her pistol to her side. You maintain the pretense of a fighting stance for a moment longer, but it becomes apparent that she has no intention of doing you any harm--at least for the moment.
--> confront_wrapup

= peaceful_confront

You stand from the desk and turn to face Sunday. You motion to the computer, where you still have the shipping manifests pulled up.

*"I know what Elysium is doing[."]," you say, trying to hold the accusation from seeping into your voice.
~whos_talking = "sunday"
~character_a = "Sunday"
"{MNC_sunday.sour:Darling|Sweetheart}, you don't even know half of it."
*"You work for Elysium; did you know about this?"[] you ask, trying to hold the accusation from seeping into your voice. {rel_sunday > 65: You want to believe she didn't know. You hope she didn't know.}
~whos_talking = "sunday"
~character_a = "Sunday"
"I did," Sunday replies solemnly.

--> confront_wrapup

= confront_wrapup

"How could you let this happen?"

The slightest hint of regret crosses her face.

~whos_talking = "sunday"
~character_a = "Sunday"
"I wasn't entirely honest with you when I told you I work for Elysium," she explains.

"What do you mean? You don't work for them?" you ask.

~whos_talking = "sunday"
~character_a = "Sunday"
"No, I do technically work for them, but..."

Sunday seems to hesitate.

*[Try to convince her she can trust you.] -> convince_sunday
*[Point out that you already know too much.] -> logic_sunday
*[Tell her that the more you know, the more you can help.] 
~rel_sunday += 5
-> help_sunday

= convince_sunday
{rel_sunday >= 80 or charm_skill >= 65 and rel_sunday >= 65:
"Sunday, come on, you can trust me," you say. "We've been through enough together, haven't we?"
She bites her lip, still uncertain, but she finally nods.
~whos_talking = "sunday"
~character_a = "Sunday"
"I was just trying to keep you out of trouble, but it looks like you've got yourself into it anyway," Sunday sighs. "All right, very well, {player_name}. I suppose I can tell you. My position at Elysium is undercover, you see. In reality I work for an organization known as the Moth Syndicate."
"Never heard of them," you say, not quite convinced.
~whos_talking = "sunday"
~character_a = "Sunday"
"Well you wouldn't have, {intro.female: smart aleck}{intro.male or intro.nonbinary: wise ass}," Sunday says, a bit short with you. "We're a shadow organization operating on the edges of society. We don't exactly advertise our existence."
"And that's... somehow better than working for a shady corporation that disappears its employees?" you ask.
~whos_talking = "sunday"
~character_a = "Sunday"
"The Moth Syndicate wants justice for those people. We may operate outside the normal boundaries of the law, but that doesn't put us on the same level as a mega-corp with no regard for the sanctity of human life." 

*[I believe Sunday. I trust her even if she works for a shadow organization.] 
~rel_sunday += 10
-> believe
*[I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
*[I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?] 
~rel_sunday -= 10
-> dont_believe
*[I don't believe Sunday. She's just lying to me now so she can kidnap me later.] 
~rel_sunday -= 10
-> dont_believe

}

{charm_skill >= 65 or rel_sunday >= 65:
"Sunday, come on, you can trust me," you say. "We've been through enough together, haven't we?"
She bites her lip, still uncertain, but she finally nods slightly.
{rel_sunday >=65:
~whos_talking = "sunday"
~character_a = "Sunday"
"I was just trying to keep you out of trouble, but it looks like you've got yourself into it anyway," Sunday sighs. <>
}
~whos_talking = "sunday"
~character_a = "Sunday"
"Very well, {player_name}. I suppose I can tell you a little bit about what's going on. My position at Elysium is undercover, you see. Truthfully I work for a... secret organization, let's call them."
"Who?" you prompt.
~whos_talking = "sunday"
~character_a = "Sunday"
"I can't tell you that. Not yet. Believe me, I wish I could, but it's complicated. But please trust me when I tell you we want justice for the people who have gone missing because of Elysium."

*[I believe Sunday. It's clear she's a kind and just person who's trying to do right, even if she works for a shady organization.] 
~rel_sunday += 10
-> believe
*[I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.] 
~rel_sunday += 10
-> believe
*[I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?]
~rel_sunday -= 10
-> dont_believe
*[I don't believe Sunday. She's just lying to me now so she can kidnap me later.]
~rel_sunday -= 10
-> dont_believe

}
{charm_skill < 65 and rel_sunday < 65:
"Sunday, come on, you can trust me," you say, but it's clear you don't really mean it. 
Sunday shakes her head and sighs.
~whos_talking = "sunday"
~character_a = "Sunday"
"Sorry, {player_name}, but I can't trust you. Not yet. There are too many players in the game, and I'm just not sure who you're representing at this point."
You open your mouth to protest, but she holds up a hand to silence you.
~whos_talking = "sunday"
"I can tell you this. I'm not here to hurt you or anyone else, and I want off this station as much as you do. I'm still willing to work together if you are."

*[I believe Sunday. It's clear she's a kind and just person who's trying to do right.]
~rel_sunday += 10
-> believe
*[I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
*[I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?] 
~rel_sunday -= 10
-> dont_believe
*[I don't believe Sunday. She's just lying to me now so she can kidnap me later.]
~rel_sunday -= 10
-> dont_believe
}


= logic_sunday

{rel_sunday >= 80 or logical_creative <= 35 and rel_sunday >= 65:
"Sunday, I already know more than I should. You can tell me," you say.
She bites her lip, still uncertain, but she finally nods.
~whos_talking = "sunday"
~character_a = "Sunday"
"I was just trying to keep you out of trouble, but it looks like you've got yourself into it anyway," Sunday sighs. "All right, very well, {player_name}. I suppose I can tell you. My position at Elysium is undercover, you see. In reality I work for an organization known as the Moth Syndicate."
"Never heard of them," you say, not quite convinced.
~whos_talking = "sunday"
~character_a = "Sunday"
"Well you wouldn't have, {intro.female: smart aleck}{intro.male or intro.nonbinary: wise ass}," Sunday says, a bit short with you. "We're a shadow organization operating on the edges of society. We don't exactly advertise our existence."
"And that's... somehow better than working for a shady corporation that disappears its employees?" you ask
~whos_talking = "sunday"
~character_a = "Sunday"
"The Moth Syndicate wants justice for those people. We may operate outside the normal boundaries of the law, but that doesn't put us on the same level as a mega-corp with no regard for the sanctity of human life." 

* [I believe Sunday. I trust her even if she works for a shadow organization.] 
~rel_sunday += 10
-> believe
* [I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
* [I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?]
~rel_sunday -= 10
-> dont_believe
* [I don't believe Sunday. She's just lying to me now so she can kidnap me later.]
~rel_sunday -= 10
-> dont_believe

}

{logical_creative <= 35 or rel_sunday >= 65:
"Sunday, I already know more than I should. You can tell me," you say.
She bites her lip, still uncertain, but she finally nods slightly.
{rel_sunday >=65:
~whos_talking = "sunday"
~character_a = "Sunday"
"I was just trying to keep you out of trouble, but it looks like you've got yourself into it anyway," Sunday sighs. <>
}
~whos_talking = "sunday"
~character_a = "Sunday"
"Very well, {player_name}. I suppose I can tell you a little bit about what's going on. My position at Elysium is undercover, you see. Truthfully I work for a... secret organization, let's call them."
"Who?" you prompt.
~whos_talking = "sunday"
~character_a = "Sunday"
"I can't tell you that. Not yet. Believe me, I wish I could, but it's complicated. But please trust me when I tell you we want justice for the people who have gone missing because of Elysium."

* [I believe Sunday. It's clear she's a kind and just person who's trying to do right, even if she works for a shady organization.]
~rel_sunday += 10
-> believe
* [I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
* [I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?] 
~rel_sunday -= 10
-> dont_believe
* [I don't believe Sunday. She's just lying to me now so she can kidnap me later.]
~rel_sunday -= 10
-> dont_believe

}
{logical_creative > 35 and rel_sunday < 65:
"Sunday, I already know more than I should. You can tell me," you say.
Sunday shakes her head and sighs.
~whos_talking = "sunday"
~character_a = "Sunday"
"Sorry, {player_name}, but I can't trust you. Not yet. There are too many players in the game, and I'm just not sure who you're representing at this point."
You open your mouth to protest, but she holds up a hand to silence you.
~whos_talking = "sunday"
~character_a = "Sunday"
"I can tell you this. I'm not here to hurt you or anyone else, and I want off this station as much as you do. I'm still willing to work together if you are."

* [I believe Sunday. It's clear she's a kind and just person who's trying to do right.]
~rel_sunday += 10
-> believe
* [I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
* [I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?] 
~rel_sunday -= 10
-> dont_believe
* [I don't believe Sunday. She's just lying to me now so she can kidnap me later.] 
~rel_sunday -= 10
-> dont_believe

}

= help_sunday

{rel_sunday >= 80 and not intro.get_phat_loots or compassion_selfserve <= 35 and rel_sunday >= 65 and not intro.get_phat_loots:
"Sunday, I want to help. If you tell me everything, I'll be better equipped to do that," you say.
She bites her lip, still uncertain, but she finally nods.
~whos_talking = "sunday"
~character_a = "Sunday"
"I was just trying to keep you out of trouble, but it looks like you've got yourself into it anyway," Sunday sighs. "All right, very well, {player_name}. I suppose I can tell you. My position at Elysium is undercover, you see. In reality I work for an organization known as the Moth Syndicate."
"Never heard of them," you say, not quite convinced.
~whos_talking = "sunday"
~character_a = "Sunday"
"Well you wouldn't have, {intro.female: smart aleck}{intro.male or intro.nonbinary: wise ass}," Sunday says, a bit short with you. "We're a shadow organization operating on the edges of society. We don't exactly advertise our existence."
"And that's... somehow better than working for a shady corporation that disappears its employees?" you ask.
~whos_talking = "sunday"
~character_a = "Sunday"
"The Moth Syndicate wants justice for those people. We may operate outside the normal boundaries of the law, but that doesn't put us on the same level as a mega-corp with no regard for the sanctity of human life." 

* [I believe Sunday. I trust her even if she works for a shadow organization.] 
~rel_sunday += 10
-> believe
* [I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
* [I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?] 
~rel_sunday -= 10
-> dont_believe
* [I don't believe Sunday. She's just lying to me now so she can kidnap me later.] 
~rel_sunday -= 10
-> dont_believe

}

{compassion_selfserve <= 35 and rel_sunday < 80 and not intro.get_phat_loots or rel_sunday >= 65 and rel_sunday < 80 and not intro.get_phat_loots:
"Sunday, I want to help. If you tell me everything, I'll be better equipped to do that," you say.
She bites her lip, still uncertain, but she finally nods slightly.
{rel_sunday >=65:
~whos_talking = "sunday"
~character_a = "Sunday"
"I was just trying to keep you out of trouble, but it looks like you've got yourself into it anyway," Sunday sighs. <>
}
~whos_talking = "sunday"
~character_a = "Sunday"
"Very well, {player_name}. I suppose I can tell you a little bit about what's going on. My position at Elysium is undercover, you see. Truthfully I work for a... secret organization, let's call them."
"Who?" you prompt.
~whos_talking = "sunday"
~character_a = "Sunday"
"I can't tell you that. Not yet. Believe me, I wish I could, but it's complicated. But please trust me when I tell you we want justice for the people who have gone missing because of Elysium."

* [I believe Sunday. It's clear she's a kind and just person who's trying to do right, even if she works for a shady organization.] 
~rel_sunday += 10
-> believe
* [I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
* [I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?]
~rel_sunday -= 10
-> dont_believe
* [I don't believe Sunday. She's just lying to me now so she can kidnap me later.]
~rel_sunday -= 10
-> dont_believe

}
{compassion_selfserve > 35 and rel_sunday < 65 or intro.get_phat_loots:
"Sunday, I want to help. If you tell me everything, I'll be better equipped to do that," you say, but it's clear you don't really mean it.
Sunday shakes her head and sighs.
~whos_talking = "sunday"
~character_a = "Sunday"
"Sorry, {player_name}, but I can't trust you. Not yet. There are too many players in the game, and I'm just not sure who you're representing at this point."
You open your mouth to protest, but she holds up a hand to silence you.
"I can tell you this. I'm not here to hurt you or anyone else, and I want off this station as much as you do. I'm still willing to work together if you are."

* [I believe Sunday. It's clear she's a kind and just person who's trying to do right.] 
~rel_sunday += 10
-> believe
* [I believe Sunday--at least until we get off this station. I can't afford to lose her as an ally.]
~rel_sunday += 10
-> believe
* [I don't believe Sunday; she's been lying to me this whole time, why should I trust her now?]
~rel_sunday -= 10
-> dont_believe
* [I don't believe Sunday. She's just lying to me now so she can kidnap me later.]
~rel_sunday -= 10
-> dont_believe
}


= dont_believe
You meet her eyes for a moment, then look away with a shake of your head.

"I'm sorry, Sunday, but I can't accept that. You're lying."

~whos_talking = "sunday"
~character_a = "Sunday"
"That's it, then?" she says. "I guess we go our separate ways now."

"I guess so," you reply.

~background = "laboratory"

The two of you exit the data room back into the laboratory. Sunday doesn't seem interested in sticking around; she exits almost immediately and without a goodbye to you or the android.

-> talking_to_lin

= believe
You meet her eyes for a moment, then nod.

"All right, Sunday, I believe you."

~whos_talking = "sunday"
~character_a = "Sunday"
"That's it, then?" she says. "I guess we should get out of here."

"I guess so," you reply.

~background = "laboratory"

The two of you exit the data room back into the laboratory. Though Sunday carries herself with her usual composure, you can tell she is relieved.

-> talking_to_lin

= talking_to_lin

Lin walks up beside you.

"I see you both found what you were looking for," they say.

{intro.get_phat_loots and came_from (->dont_believe):
"Actually, about that..." you say, glancing over at them. "I've authorized departures for short-range shuttles, but I'm still keen on starting my new life ahead of the game once I get out of here, if you know what I mean."
"Yes, I can intuit that you are still interested in material gain," Lin says. "Rest assured that I possess many multitudes of delicate data and information that I am sure we could find a suitably generous buyer for--once we are out of here?"
 }
 {not intro.get_phat_loots and came_from (->dont_believe):
 "Not sure she did," you reply. "It might be a problem after I get off Cassio."
 
 Lin considers this.
 
 "Perhaps I could be of assistance--once we are out of here?"
}

{came_from (->believe):
~whos_talking = "sunday"
~character_a = "Sunday"
"We did. Thank you for your assistance," Sunday says kindly.

Lin considers this.

"Perhaps I could be of more assistance--once we are out of here?"
}

-The uptick in the android's voice at the end indicates the question they are asking--whether they would be joining {came_from (->believe): the two of} you or not.

"I don't see why not," you say.

Lin nods.

After a moment you note that you are likely running out of time before the station's inevitable demise, and so {came_from (->believe): the three of you|the two of you} exit the laboratory without further discussion.

* [Next]
-> transitions.priority_update
